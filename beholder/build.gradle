apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


def bintrayUser = ""
def bintrayKey = ""
try {
    bintrayUser = project.property("bintray.user")
    bintrayKey = project.property("bintray.key")
    println bintrayUser
    println bintrayKey

} catch (Exception ignore) {
    println "Missing release keystore passwords, add the keys \"bintray.user\" and \"bintray.key\" to your gradle.properties file"
}

def niceVersion = "0.3.5"
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName = niceVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        jackOptions.enabled = false
        dataBinding.enabled = true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    testCompile 'junit:junit:4.12'
}


def siteUrl = 'https://github.com/MazyNoc/beholder-lib'    // Homepage URL of the library
def gitUrl = 'https://github.com/MazyNoc/beholder-lib.git' // Git repository URL
group = "nu.annat.android"                               // Maven Group ID for the artifact
version = niceVersion

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            artifactId = "beholder"
            project {
                packaging 'aar'

                // Add your description here
                name 'Simplifying Recylerviews with mixed content'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'MazyNoc'
                        name 'Mikael Alfredsson'
                        email 'MazyNoc@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key =  bintrayKey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "beholder"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}


import org.ajoberstar.gradle.git.tasks.GitPush
import org.ajoberstar.gradle.git.tasks.GitTag

task createTag(type: GitTag) {
    repoPath = rootDir
    tagName = niceVersion
    message = "Application release ${project.version.toString()}"
}

task pushTag(type: GitPush, dependsOn: createTag) {
    namesOrSpecs = [niceVersion]
}

bintrayUpload.dependsOn pushTag
